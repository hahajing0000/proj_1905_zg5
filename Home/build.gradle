if (isHomeComponent.toBoolean()){
    apply plugin: 'com.android.application'
}else{
    apply plugin: 'com.android.library'
}


android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    //资源加前缀
    resourcePrefix "home_"

    sourceSets{
        main{
            if (isHomeComponent.toBoolean()){
                manifest.srcFile 'src/main/component/AndroidManifest.xml'
            }
            else{
                manifest.srcFile 'src/main/AndroidManifest.xml'
            }
        }
    }

    dataBinding{
        enabled=true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    flavorDimensions "channcel"
    productFlavors{
        xiaomi{
            manifestPlaceholders = [CHANNEL_VALUE: "xiaomi"]

        }

        huawei{
            manifestPlaceholders = [CHANNEL_VALUE: "huawei"]
        }

        oppo{
            manifestPlaceholders = [CHANNEL_VALUE: "oppo"]
        }
    }

    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation project(path: ':Common')
    implementation project(path: ':Net')
    implementation project(path: ':MVVMCore')
    implementation project(path: ':Utils')
    implementation project(path: ':Resource')
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation project(path: ':Wiget')
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    api 'com.tencent.tbs:tbssdk:44085'

    // Java language implementation
    implementation "androidx.biometric:biometric:1.1.0"
}